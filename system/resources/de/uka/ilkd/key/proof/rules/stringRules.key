\rules{
  /***********
   *  AXIOMS  *
   ***********/
  /*
   * CharList translateInt (int)
   * - translating an integer into a CharList
   */
  translate0 {
    \schemaVar \term numbers iz;
    \find (translateInt(Z(0(iz))))
    \replacewith (concat(translateInt(Z(iz)),cons('0',empty)))
    \heuristics (integerToString)
  };

  translate1 {
    \schemaVar \term numbers iz;
    \find (translateInt(Z(1(iz))))
    \replacewith (concat(translateInt(Z(iz)),cons('1',empty)))
    \heuristics (integerToString)
  };

  translate2 {
    \schemaVar \term numbers iz;
    \find (translateInt(Z(2(iz))))
    \replacewith (concat(translateInt(Z(iz)),cons('2',empty)))
    \heuristics (integerToString)
  };

  translate3 {
    \schemaVar \term numbers iz;
    \find (translateInt(Z(3(iz))))
    \replacewith (concat(translateInt(Z(iz)),cons('3',empty)))
    \heuristics (integerToString)
  };

  translate4 {
    \schemaVar \term numbers iz;
    \find (translateInt(Z(4(iz))))
    \replacewith (concat(translateInt(Z(iz)),cons('4',empty)))
    \heuristics (integerToString)
  };

  translate5 {
    \schemaVar \term numbers iz;
    \find (translateInt(Z(5(iz))))
    \replacewith (concat(translateInt(Z(iz)),cons('5',empty)))
    \heuristics (integerToString)
  };

  translate6 {
    \schemaVar \term numbers iz;
    \find (translateInt(Z(6(iz))))
    \replacewith (concat(translateInt(Z(iz)),cons('6',empty)))
    \heuristics (integerToString)
  };

  translate7 {
    \schemaVar \term numbers iz;
    \find (translateInt(Z(7(iz))))
    \replacewith (concat(translateInt(Z(iz)),cons('7',empty)))
    \heuristics (integerToString)
  };

  translate8 {
    \schemaVar \term numbers iz;
    \find (translateInt(Z(8(iz))))
    \replacewith (concat(translateInt(Z(iz)),cons('8',empty)))
    \heuristics (integerToString)
  };

  translate9 {
    \schemaVar \term numbers iz;
    \find (translateInt(Z(9(iz))))
    \replacewith (concat(translateInt(Z(iz)),cons('9',empty)))
    \heuristics (integerToString)
  };

  translate# {
    \find (translateInt(Z(#)))
    \replacewith (empty)
    \heuristics (integerToString)
  };

  translateNegLit {
    \schemaVar \term numbers iz;
    \find (translateInt(Z(neglit(iz))))
    \replacewith (concat(translateInt(Z(iz)),cons('-',empty)))
    \heuristics (integerToString)
  };

  
  /*
   * CharList removeZeros (CharList) - removing '0' from
   * the beginning of a CharList must not lead to the empty list
   */
  removeZeros {
    \schemaVar \term CharList l;
    \schemaVar \term jchar c1, c2;
    \find (removeZeros(cons(c1,cons(c2,l))))
    \replacewith (\if (c1 = '0') \then (removeZeros(cons(c2,l)))
    		      	   	 \else (cons(c1,cons(c2,l))))
    \heuristics (integerToString)
  };

  removeZerosEmpty {
    \schemaVar \term jchar c;
    \find (removeZeros(cons(c,empty)))
    \replacewith (cons(c,empty))
    \heuristics (integerToString)
  };


  /*
   * int length(CharList)
   */
  lengthEmpty {
    \find (length(empty))
    \replacewith (0)
    \heuristics (simplify)
  };

  lengthCons {
    \schemaVar \term CharList l;
    \schemaVar \term jchar c;
    \find ( length(cons(c,l)) )
    \replacewith ( length(l) + 1 )
    \heuristics ( stringReduceHighTerminating )
  };

  /*
   * int charAt (int,CharList)
   * an index < 0 and >= length(l) in unspecified
   */
  charAt0 {
    \schemaVar \term CharList l;
    \schemaVar \term jchar c;
    \find (charAt(0,cons(c,l)))
    \replacewith (c)
    \heuristics ( stringReduceHighTerminating )
  };

  charAtI {
    \schemaVar \term CharList l;
    \schemaVar \term jchar c;
    \schemaVar \term int i;
    \find (charAt(i,cons(c,l)))
    \replacewith (\if (i>0) \then (charAt(i-1,l)) 
		            \else (
		                \if ( i = 0 ) 
		                \then (c) 
		                \else (charAt(i,cons(c,l)))
		             )
		 )
    \heuristics ( stringReduceHigh )
  };

  /*
   * = (CharList,CharList)
   * the equality predicate
   */
  equalFalse1 {
    \schemaVar \term CharList l;
    \schemaVar \term jchar c;
    \find (empty = cons(c,l))
    \replacewith (false)
    \heuristics ( stringReduceHighTerminating )
  };

  equalFalse2 {
    \schemaVar \term CharList l;
    \schemaVar \term jchar c;
    \find (cons(c,l) = empty)
    \replacewith (false)
    \heuristics ( stringReduceHighTerminating )
  };

  equalReduce {
    \schemaVar \term CharList l1, l2;
    \schemaVar \term jchar c;
    \find (cons(c,l1) = cons(c,l2))
    \replacewith (l1 = l2)
    \heuristics ( stringReduceHighTerminating ) 
  };

  equalFalse {
    \schemaVar \term CharList l1, l2;
    \schemaVar \term jchar c1, c2;
    \find (cons(c1,l1) = cons(c2,l2))
    \replacewith (\if (c1 = c2) \then (l1 = l2)
    		      	    	\else (false))
    \heuristics ( stringReduceHighTerminating ) 
  };
  
  equalCharacters {
    \schemaVar \term numbers iz1, iz2;
    \find ( C(iz1) = C(iz2) )
    \replacewith ( Z(iz1) = Z(iz2) )
    \heuristics ( stringReduceHighTerminating ) 
  };
    
  /*
   * Equality is also axiomatized using charAt...
   */
  stringEqualityDefinition {
    \schemaVar \term CharList l1, l2;
    \schemaVar \variables int iv;
    \find ( l1 = l2 )
    \varcond ( \notFreeIn(iv,l1), \notFreeIn(iv,l2) )
    \replacewith ( length(l1) = length(l2)
               & \forall iv; ((iv >= 0 & iv < length(l1))
                               -> charAt(iv,l1) = charAt(iv,l2)) )
  };

  /*
   * structural induction over all CharLists
   */
  charlist_induction {
    \schemaVar \variables CharList lv;
    \schemaVar \variables jchar cv;
    \schemaVar \formula phi;
    \varcond ( \notFreeIn(cv,phi)  )
    "Base Case": \add ( ==> {\subst lv; empty}(phi) );
    "Step Case": \add ( ==> \forall lv; \forall cv;
                              ((inChar(cv) & phi)
                               -> {\subst lv; cons(cv,lv)}phi) );
    "Use Case":  \add ( \forall lv; (phi) ==> )
  };

  /*
   * jchar indexOf (jchar,int,CharList)
   */
  indexOf {
    \schemaVar \term CharList l;
    \schemaVar \term jchar c;
    \schemaVar \term int i;
    \schemaVar \variables int iv;
    \find (indexOf (c,i,l))
    \varcond ( \notFreeIn(iv,l),
               \notFreeIn(iv,c),
               \notFreeIn(iv,i) )
    \replacewith ( \ifEx iv; (  iv >= 0
                              & iv >= i
                              & iv < length(l)
                              & charAt(iv,l) = c )
                     \then (iv)
                     \else (-1) )
  };

  /*
   * CharList substring(int,int,CharList)
   * from start index to end index
   * The axiom substring0 uses the indirection through concat,
   * so it does not create a cons term that is not in value form.
   */
  substring0 {
    \schemaVar \term CharList l;
    \schemaVar \term int i;
    \schemaVar \term jchar c;
    \find ( substring (0, i, cons(c,l)) )
    \replacewith ( \if (i > 0)
                   \then (concat(cons(c, empty), substring(0, i - 1, l)))
                   \else (substring(0, i, cons (c, l))) )
    \heuristics ( stringReduceHigh )
  };

  substringI {
    \schemaVar \term CharList l;
    \schemaVar \term jchar c;
    \schemaVar \term int i,j;
    \find ( substring (i,j,cons(c,l)) )
    \replacewith ( \if (i>0 & i<=j) \then (substring(i-1,j-1,l))
    		       	     \else (substring(i,j,cons(c,l))) )
    \heuristics ( stringNormalisation1 )
  };

  substring00 {
    \schemaVar \term CharList l;
    \find ( substring(0,0,l) )
    \replacewith ( empty )
    \heuristics ( concrete )
  };

  
  /*
   * endsWith(CharList,CharList)
   * predicate indicating if the first list
   * is a suffix of the second one
   */
  endsWith {
    \schemaVar \term CharList l1, l2;
    
    \find (endsWith(l1,l2))
    \replacewith ( \if (length(l1) > length(l2))
    		   \then (false)
		   \else (substring(length(l2)-length(l1),
                                    length(l2),l2) = l1) )
    
    \heuristics (stringNormalisationReduce)
  };

  /*
   * startsWith(CharList,CharList)
   * predicate indicating if the first list
   * is a prefix of the second one
   */
  startsWith {
    \schemaVar \term CharList l1,l2;
    \find(startsWith(l1,l2))
    \replacewith ( \if (length(l1)>length(l2))
    		   \then (false)
		   \else (substring(0,length(l1),l2) = l1) )
    
    \heuristics (stringNormalisationReduce)
  };

  /*
   * CharList concat (CharList,CharList)
   * concatenate the two lists
   */
  concatEmpty1 {
    \schemaVar \term CharList l;
    \find (concat(empty,l))
    \replacewith (l)
    \heuristics (simplify)
  };

  concatEmpty2 {
    \schemaVar \term CharList l;
    \find (concat(l,empty))
    \replacewith (l)
    \heuristics (simplify)
  };

/** concat should not built terms, where a cons subterm is
     not in value form so this axiom should be used with care **/
  concatConsDep {
    \schemaVar \term CharList l1, l2;
    \schemaVar \term jchar c;

    \find (concat(cons(c,l1),l2))
    \replacewith (cons(c,concat(l1,l2)))
  };

  concatCons {
    \schemaVar \term CharList l1, l2;
    \schemaVar \term jchar c1, c2;

    \find (concat(cons(c1,l1),cons(c2,l2)))
    \replacewith (cat(cons(c1,empty),concat(l1,cons(c2,l2))))

    \heuristics ( stringReduceHighTerminating )
  };

 
     
  cat {
    \schemaVar \term CharList l;
    \schemaVar \term jchar c1, c2;
    
    \find (cat(cons(c1,empty),cons(c2,l)))
    \replacewith (cons(c1,cons(c2,l)))
    
    \heuristics ( stringReduceHighTerminating )
  };

  /*
   * contains (CharList,CharList)
   * predicate indicating whether the first list
   * is contained in the second
   */
  containsAxiomAntec {
    \schemaVar \term CharList searchString, textString;
    \schemaVar \variables int iv;

    \find ( contains(searchString, textString) ==> )
    \varcond ( \notFreeIn (iv, searchString), \notFreeIn (iv, textString) )
    \replacewith ( \exists iv; (  iv >= 0
                               & iv + length(searchString) <= length(textString)
                               & substring(iv, iv + length(searchString), textString) = searchString) ==> )
    
    \heuristics (stringNormalisation1)
  };

  containsAxiomSucc {
    \schemaVar \term CharList searchString, textString;
    \schemaVar \variables int iv;

    \find (==> contains(searchString, textString) )
    \varcond ( \notFreeIn (iv, searchString), \notFreeIn (iv, textString) )
    \replacewith ( ==> \exists iv; (  iv >= 0
                       	            & iv + length(searchString) <= length(textString)
                                    & substring(iv, iv + length(searchString), textString) = searchString) )
    
    \heuristics (stringNormalisation1)
  };
  
  contains {
    \schemaVar \term CharList searchString, textStringTail;
    \schemaVar \term jchar fstTextCharacter;
    \schemaVar \skolemTerm int lengthSearchString;    
    
    \find (contains(searchString, cons(fstTextCharacter,textStringTail) ) )
    \sameUpdateLevel
    \varcond ( \new(lengthSearchString, \dependingOn(searchString) ) )
    
    \replacewith ( lengthSearchString < length(textStringTail) & 
                     ( substring(0, lengthSearchString, cons(fstTextCharacter,textStringTail)) = searchString | 
                       contains(searchString, textStringTail) ) )
    \add ( length(searchString) = lengthSearchString ==>)
                               
    \heuristics (stringNormalisationReduce)
    
  };

  /*
   * int indexOfStr (CharList,int,CharList)
   */
  indexOfStr {
    \schemaVar \term CharList l1, l2;
    \schemaVar \term int i;
    \schemaVar \variables int iv;
    \find (indexOfStr(l1,i,l2))
    \varcond ( \notFreeIn (iv,l1), \notFreeIn (iv,l2), \notFreeIn (iv,i) )
    \replacewith ( \ifEx iv; (iv >= i
    		   	      & iv >= 0
    		   	      & iv+length(l1) <= length(l2)
			      & (substring(iv,iv+length(l1),l2) = l1))
                   \then (iv)
                   \else (-1) )
    \heuristics (stringNormalisationReduce)    
  };

  /*
   * int lastIndexOf (jchar,int,CharList)
   */
  lastIndexOf {
      \schemaVar \term CharList l;
      \schemaVar \term jchar c;
      \schemaVar \term int i;
      \schemaVar \variables int iv;
      \find (lastIndexOf (c,i,l))
      \varcond ( \notFreeIn (iv,c), \notFreeIn (iv,i), \notFreeIn (iv,l) )
      \replacewith (\ifEx iv; (  iv > 0
                                & i-iv >= 0
                                & i-iv < length(l)
                                & charAt(i-iv,l) = c)
                    \then (i - iv)
                    \else (-1))
      \heuristics (stringNormalisationReduce)  
  };

  /*
   * int lastIndexOfStr (CharList,int,CharList)
   */
  lastIndexOfStr {
      \schemaVar \term CharList l1,l2;
      \schemaVar \term int i;
      \schemaVar \variables int iv;
      \find (lastIndexOfStr (l1,i,l2))
      \varcond ( \notFreeIn (iv,l1), \notFreeIn (iv,i), \notFreeIn (iv,l2) )
      \replacewith (\ifEx iv; (  iv > 0
                                & i-iv >= 0
                                & length(l1)+i-iv <= length(l2)
                                & (substring(i-iv,length(l1)+i-iv,l2) = l1))
                    \then (i - iv)
                    \else (-1))
      \heuristics (stringNormalisationReduce)
  };

  /*
   * CharList replace (jchar,jchar,CharList)
   * replace all occurences of the first character
   * with the second character
   */
  replaceEmpty {
    \schemaVar \term jchar c1, c2;
    \find (replace(c1,c2,empty))
    \replacewith (empty)
    \heuristics (simplify)
  };

  replaceCons {
    \schemaVar \term CharList l;
    \schemaVar \term jchar c1, c2, c3;
    \find (replace(c1,c2,cons(c3,l)))
    \replacewith ( \if (c1 = c3)
                   \then (concat(cons(c2,empty),
                                 replace(c1,c2,l)))
                   \else (concat(cons(c3,empty),
                                 replace(c1,c2,l))) )
    \heuristics ( stringReduceHighTerminating )
  };

  /**
   * we define only the base case
   */
  hashCodeBase {
    \find (hashCode(empty))
    \replacewith ( 0 )
    \heuristics (concrete)
  };

  /************
   *  LEMMAS  *
   ************/
   
  substringIdentity0 {
    \schemaVar \term CharList l;
    \schemaVar \term int len;    
    \assumes (length(l) = len ==>)
    \find ( substring(0,len,l) )
    \sameUpdateLevel
    \replacewith ( l )
    \heuristics ( stringReduceHighTerminating )
    \displayname "substringIdentity"
  };


  substringIdentity1 {
    \schemaVar \term CharList str;
    \find ( substring(0, length(str), str) )
    \replacewith ( str )
    \heuristics ( stringReduceHighTerminating )
    \displayname "substringIdentity"
  };

   /*
    * empty is the only list having length 0
    */
   zeroLengthIsEmptyString {
    \schemaVar \term CharList str;
    \assumes (length(str) = 0 ==> )
    \find ( str )
    \replacewith ( empty )
    \heuristics ( stringsLengthReduce, stringDiffIfFind )
  };

  /*
   * The length of every CharList is at least 0
   */
  lengthNonNegative {
      \schemaVar \term CharList str;
      \find (length(str)) 
      \sameUpdateLevel
      \add (length(str) >= 0 ==> )
      \heuristics ( stringsLengthInvariant, stringsLengthInvariantNoLiteral )
  };  
  
  /*
   * The length of a well defined substring is
   * endIndex-startIndex
   */
  lengthSubstring {
      \schemaVar \term CharList str;
      \schemaVar \term int startIdx, endIdx;
      
      \find (length(substring(startIdx, endIdx, str)))
      \replacewith (\if (startIdx >= 0 & endIdx >= startIdx & endIdx <= length(str))
                    \then ( endIdx - startIdx )
                    \else ( length ( substring(startIdx, endIdx, str) ) ) )
      \heuristics ( stringReduceHigh )
  };
  
  equalStringsLengthEq {
    \schemaVar \term CharList str, subStr;
    \schemaVar \term int startIdx, endIdx;


    \find ( substring(startIdx, endIdx, str) = subStr ==> )
    \add ( 
       (startIdx >= 0 & startIdx <= endIdx & endIdx <= length(str) ) 
       ->  
       (endIdx - startIdx) = length(subStr) ==>
     )
    \heuristics ( equalityStrings )
  };

  /*
   * The length of a concatenation
   */
  lengthConcat {
      \schemaVar \term CharList leftStr, rightStr;
      
      \find ( length(concat(leftStr, rightStr)) )
      \replacewith ( length(leftStr) + length(rightStr) )
      
      \heuristics ( stringsReduceConcat )
  };

  lengthConcat2 {
      \schemaVar \term CharList leftStr, rightStr, concatStr;
      
      \assumes ( concat(leftStr, rightStr) = concatStr ==>)
      \find ( length(concatStr) )
      \sameUpdateLevel      
      \replacewith ( length(leftStr) + length(rightStr) )
      \heuristics ( stringsConcatNotBothLiterals, stringsReduceConcat )
      \displayname "lengthConcat"
  };


  /*
   * A replace application will not change
   * the length of the string
   */
  lengthReplace {
      \schemaVar \term CharList l;
      \schemaVar \term jchar c1, c2;
      \find (length(replace(c1,c2,l)))
      \replacewith(length(l))
      \heuristics ( stringsReduceReplace )
  };

  /*
   * A replace application will not change
   * the length of the string
   */
  lengthReplace2 {
      \schemaVar \term CharList l,s;
      \schemaVar \term jchar c1, c2;
      \assumes (replace(c1,c2,l) = s ==>)
      \find (length(s))
      \sameUpdateLevel      
      \replacewith(length(l))
      \heuristics ( stringsReduceReplace )
      \displayname "lengthReplace"
  };


  /*
   * In a term charAt(i,substring(j,k,l)), the substring
   * can be omitted, when adjusting the index
   */
  charAtSubstring1 {
      \schemaVar \term CharList str;
      \schemaVar \term int charPos,startIdx,endIdx;

      \find (charAt(charPos, substring(startIdx, endIdx, str)))
      \replacewith( \if (  charPos<endIdx-startIdx
                         & charPos>=0
                         & startIdx>=0
                         & endIdx>=startIdx
                         & endIdx<=length(str))
                    \then (charAt(charPos + startIdx, str))
                    \else (charAt(charPos, substring(startIdx,endIdx,str))))
     \heuristics ( stringsReduceSubstring )    
     \displayname "charAtSubstring"
  };

  charAtSubstring2 {
      \schemaVar \term CharList str, subStr;
      \schemaVar \term int charPos,startIdx,endIdx;

      \assumes ( substring(startIdx,endIdx,str) = subStr ==> )
      \find ( charAt(charPos, subStr) )
      \sameUpdateLevel
      \replacewith ( \if (charPos<endIdx-startIdx
                         & charPos>=0
                         & startIdx>=0
                         & endIdx>=startIdx
                         & endIdx<=length(str))
                    \then (charAt(charPos+startIdx,str))
                    \else (charAt(charPos,subStr)) )
     \heuristics ( stringsReduceSubstring )    
     \displayname "charAtSubstring"
  };


  /*
   * charAt applied on a concat will either refer
   * to the first or the second subterm
   */
  charAtConcat {
      \schemaVar \term CharList leftStr,rightStr;
      \schemaVar \term int charPos;
      
      \find ( charAt(charPos, concat(leftStr, rightStr)))
      \replacewith ( \if (charPos >= 0 & charPos < length(leftStr) + length(rightStr))
                     \then (\if (charPos < length(leftStr))
                             \then (charAt(charPos, leftStr))
                             \else (charAt(charPos - length(leftStr), rightStr)))
                     \else (charAt(charPos, concat(leftStr, rightStr))) )
      \heuristics ( stringsConcatNotBothLiterals, stringsReduceConcat )
  };

  /*
   * Two succedent substring calls can be combined to one
   */
  substringSubstring {
      \schemaVar \term CharList str;
      \schemaVar \term int outerStartIdx, outerEndIdx, innerStartIdx, innerEndIdx;
      \find ( substring(outerStartIdx, outerEndIdx,substring(innerStartIdx, innerEndIdx, str)) )
      \replacewith ( \if (  innerStartIdx >= 0 
      			  & innerEndIdx >= innerStartIdx
                          & innerEndIdx <= length(str)
                          & outerStartIdx >= 0
                          & outerEndIdx >= outerStartIdx 
                          & outerEndIdx <= innerEndIdx - innerStartIdx)
                     \then (substring(outerStartIdx + innerStartIdx, innerStartIdx + outerEndIdx, str))
                     \else (substring(outerStartIdx, outerEndIdx, substring(innerStartIdx,innerEndIdx, str))) )
      \heuristics ( stringsReduceSubstring )      
  };


  substringSubstring2 {
      \schemaVar \term CharList str, innerSubStr;
      \schemaVar \term int outerStartIdx, outerEndIdx, innerStartIdx, innerEndIdx;

      \assumes ( substring(innerStartIdx, innerEndIdx, str) = innerSubStr ==> )

      \find ( substring(outerStartIdx, outerEndIdx, innerSubStr) )
      \replacewith ( \if (  innerStartIdx >= 0 
      			  & innerEndIdx >= innerStartIdx
                          & innerEndIdx <= length(str)
                          & outerStartIdx >= 0
                          & outerEndIdx >= outerStartIdx 
                          & outerEndIdx <= innerEndIdx - innerStartIdx)
                     \then (substring(outerStartIdx + innerStartIdx, innerStartIdx + outerEndIdx, str))
                     \else (substring(outerStartIdx, outerEndIdx, innerSubStr)) 
                    ) 
      \displayname "substringSubstring"      
  };


  substringConcat {
      \schemaVar \term CharList leftStr, rightStr;
      \schemaVar \term int startIdx, endIdx;
      
      \find ( substring(startIdx, endIdx, concat(leftStr, rightStr)) )
      \replacewith ( \if (  startIdx >= 0 & endIdx >= startIdx
                          & endIdx <= length(leftStr) + length(rightStr))
                     \then (\if (endIdx <= length(leftStr))
                            \then (substring(startIdx, endIdx, leftStr))
                            \else (\if (startIdx >= length(leftStr))
                                   \then (substring(
                                           startIdx - length(leftStr),
                                           endIdx - length(leftStr),rightStr))
                                   \else (concat(
                                           substring(startIdx,
                                                     length(leftStr),
                                                     leftStr),
                                           substring(0,
                                                     endIdx - length(leftStr),
                                                     rightStr)))))
                     \else (substring(startIdx, endIdx, concat(leftStr, rightStr))) )
      \heuristics ( stringsReduceOrMoveOutsideConcat, stringsConcatNotBothLiterals )      
  };

  substringConcat2 {
      \schemaVar \term CharList leftStr, rightStr, concatStr;
      \schemaVar \term int startIdx, endIdx;

      \assumes ( concat(leftStr, rightStr) = concatStr ==> )      
      \find ( substring(startIdx, endIdx, concatStr) )
      \replacewith ( \if (  startIdx >= 0 & endIdx >= startIdx
                          & endIdx <= length(concatStr) )
                     \then (\if (endIdx <= length(leftStr))
                            \then (substring(startIdx, endIdx, leftStr))
                            \else (\if (startIdx >= length(leftStr))
                                   \then (substring(
                                           startIdx - length(leftStr),
                                           endIdx - length(leftStr),rightStr))
                                   \else (concat(
                                           substring(startIdx,
                                                     length(leftStr),
                                                     leftStr),
                                           substring(0,
                                                     endIdx - length(leftStr),
                                                     rightStr)))))
                     \else (substring(startIdx, endIdx, concatStr)) )
      \heuristics ( stringsReduceOrMoveOutsideConcat, stringsConcatNotBothLiterals )      
  
      \displayname "substringConcat"
  };

  concatConcat {
      \schemaVar \term CharList outerLeft, innerLeft, innerRight;
      \find ( concat(outerLeft, concat(innerLeft, innerRight)) )
      \replacewith ( concat(concat(outerLeft, innerLeft), innerRight) )
      \heuristics ( stringsConcatAssoc )      
  };


// CONTINUE HERE

 concat2SubLeft {
     \schemaVar \term CharList concatenatedString, concatLeft, concatRight;

     \find ( concat(concatLeft, concatRight) = concatenatedString )
     \replacewith (concatLeft = substring(0, length(concatLeft), concatenatedString) &
     	concatRight = substring(length(concatLeft), length(concatLeft) + length(concatRight), concatenatedString) & 
     	length(concatLeft) + length(concatRight) = length(concatenatedString))

     \heuristics ( stringNormalisation1 )
  };

  concat2SubRight {
     \schemaVar \term CharList concatenatedString, concatLeft, concatRight;

     \find ( concatenatedString = concat(concatLeft, concatRight) )
     \replacewith (
        concatLeft = substring(0, length(concatLeft), concatenatedString) & 
     	concatRight = substring(length(concatLeft), length(concatLeft) + length(concatRight), concatenatedString) & 
     	length(concatLeft) + length(concatRight) = length(concatenatedString))

     \heuristics ( stringNormalisation1 )
  };

  replaceSubstring1 {
      \schemaVar \term CharList str;
      \schemaVar \term jchar searchChar, replaceChar;
      \schemaVar \term int startIdx, endIdx;

      \find (replace(searchChar, replaceChar, substring(startIdx, endIdx, str)))
      \replacewith (\if (startIdx >= 0 & endIdx >= startIdx & endIdx <= length(str))
                    \then (substring(startIdx, endIdx, replace(searchChar, replaceChar, str)))
                    \else (replace(searchChar, replaceChar, substring(startIdx, endIdx, str))) )
      \heuristics ( stringMoveReplaceInside )
      
      \displayname "replaceSubstring"      
  };

  replaceSubstring2 {
      \schemaVar \term CharList str, subStr;
      \schemaVar \term jchar searchChar, replaceChar;
      \schemaVar \term int startIdx, endIdx;

      \assumes (substring(startIdx, endIdx, str) = subStr ==>)

      \find (replace(searchChar, replaceChar, subStr))
      \replacewith (\if (startIdx >= 0 & endIdx >= startIdx & endIdx <= length(str))
                    \then (substring(startIdx, endIdx, replace(searchChar, replaceChar, str)))
                    \else (replace(searchChar, replaceChar, subStr)) )
      \heuristics ( stringMoveReplaceInside )      

      \displayname "replaceSubstring"      
  };

  /*
   * A replace inside a concat application
   * does not affect the concat...
   */
  replaceConcat {
      \schemaVar \term CharList leftStr, rightStr;
      \schemaVar \term jchar searchChar, replaceChar;
      \find (replace(searchChar, replaceChar, concat(leftStr, rightStr)))
      \replacewith (concat(replace(searchChar, replaceChar, leftStr),
                           replace(searchChar, replaceChar, rightStr)))
      \heuristics ( stringMoveReplaceInside )      
   };

  /*
   * l is not equal to cons(c,l)
   */
  consEq {
      \schemaVar \term CharList l;
      \schemaVar \term jchar c;
      \find ( l = cons(c,l) )
      \replacewith ( false )
      \heuristics (concrete)
  };

  /*
   * concatenation of a non empty string
   * changes the original string
   */
  concatEqLeft {
      \schemaVar \term CharList l1, l2;
      \assumes ( ==> l2 = empty)
      \find ( concat(l1,l2) = l1 )
      \sameUpdateLevel
      \replacewith ( false )
      \heuristics (concrete)
  };
  
}
